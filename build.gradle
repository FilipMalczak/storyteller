plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group = "com.github.filipmalczak"
version = '0.0.1-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencyManagement {
    dependencies {
        dependency 'org.projectlombok:lombok:1.18.20'
        dependency 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        dependency 'org.junit.jupiter:junit-jupiter-engine:5.8.1'


        dependency 'org.dizitart:nitrite:3.4.4'

        dependency 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2'


        dependency group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
        dependency 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
        dependency 'org.apache.logging.log4j:log4j-api:2.14.1'

        dependency 'commons-io:commons-io:2.11.0'


        dependency 'com.google.flogger:flogger-slf4j-backend:0.7.2'
        dependency 'com.google.flogger:flogger:0.7.2'

        dependency 'org.valid4j:valid4j:0.5.0'

    }

}

dependencies {
    //impl
    implementation 'org.dizitart:nitrite'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.valid4j:valid4j'

    //testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'commons-io:commons-io'

    //logging
    implementation 'com.google.flogger:flogger'
    //fixme this is an implementation dependency, so that consumers only have to work against SLF4J config, but OTOH, it requires a system property...
    implementation 'com.google.flogger:flogger-slf4j-backend'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
    systemProperty("flogger.backend_factory", "com.google.common.flogger.backend.slf4j.Slf4jBackendFactory#getInstance")
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

wrapper {
    gradleVersion = "7.1.1"
    distributionType = Wrapper.DistributionType.ALL
}
